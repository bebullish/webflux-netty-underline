plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'maven-publish'
}

group = 'cn.bebullish'
version = '1.0.0'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

publishing {
    repositories {
        maven {
            url = codingArtifactsRepoUrl
            credentials {
                username = codingArtifactsGradleUsername
                password = codingArtifactsGradlePassword
            }
            name 'coding'
        }
        maven {
            url = bebullishArtifactsRepoUrl
            credentials {
                username = bebullishArtifactsGradleUsername
                password = bebullishArtifactsGradlePassword
            }
            name 'bebullish'
        }
    }
    publications {
        marlon(MavenPublication) {
            groupId = "$project.group"
            artifactId = "$rootProject.name"
            version = "$project.version"

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "$rootProject.name"
                description = 'Netty container-based Spring WebFlux application with URL HOST support for underscored HTTP requests'
                url = 'https://github.com/bebullish/webflux-netty-underline.git'
                developers {
                    developer {
                        id = 'bebullish'
                        name = 'bebullish'
                        email = '15048969966@163.com'
                    }
                }
            }
        }
    }
}